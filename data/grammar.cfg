# --- Văn phạm CFG cho Bài tập lớn NLP (CO3085) ---
# Miền: Đặt món ăn online

# --- Ký hiệu bắt đầu ---
S -> S_ORDER | S_QUERY | S_CANCEL | S_ADD | S_MODIFY | S_CONFIRM
S -> S_ORDER PUNC_END
S -> S_QUERY PUNC_Q
S -> S_CANCEL PUNC_END
S -> S_ADD PUNC_END
S -> S_MODIFY PUNC_END
S -> S_CONFIRM PUNC_END

# --- Dấu câu ---
PUNC_END -> '.' | '!'
PUNC_Q -> '?' | '?!'

# --- Các loại câu ---
S_ORDER -> GREET VP_ORDER | VP_ORDER
S_ORDER -> GREET VP_ORDER POLITENESS | VP_ORDER POLITENESS

S_QUERY -> GREET VP_QUERY | VP_QUERY
S_QUERY -> GREET VP_QUERY POLITENESS | VP_QUERY POLITENESS

S_CANCEL -> GREET VP_CANCEL | VP_CANCEL
S_CANCEL -> GREET VP_CANCEL POLITENESS | VP_CANCEL POLITENESS

S_ADD -> GREET VP_ADD | VP_ADD
S_ADD -> GREET VP_ADD POLITENESS | VP_ADD POLITENESS

S_MODIFY -> GREET VP_MODIFY | VP_MODIFY
S_MODIFY -> GREET VP_MODIFY POLITENESS | VP_MODIFY POLITENESS

S_CONFIRM -> GREET VP_CONFIRM | VP_CONFIRM

# --- Lời chào ---
GREET -> SUBJ 'muốn' | 'cho' SUBJ | 'làm' 'ơn' | 'xin' 'chào' | 'chào' 'bạn' | 'alo'

# --- Chủ ngữ ---
SUBJ -> 'tôi' | 'em' | 'mình' | 'anh' | 'chị' | 'cô' | 'chú' | 'bác' | 'con'

# --- Lời kết ---
POLITENESS -> 'nhé' | 'ạ' | 'nha' | 'nhé' 'ạ' | 'cảm' 'ơn' | 'ạ' 'cảm' 'ơn'

# --- Cụm động từ: Đặt món ---
VP_ORDER -> V_ORDER NP_DISH
VP_ORDER -> V_ORDER NP_DISH PP_TIME
VP_ORDER -> V_ORDER NP_DISH PP_LOCATION
VP_ORDER -> V_ORDER NP_DISH PP_TIME PP_LOCATION
VP_ORDER -> V_ORDER NP_DISH 'và' NP_DISH
VP_ORDER -> V_ORDER NP_DISH ',' NP_DISH
VP_ORDER -> V_ORDER NP_DISH ',' NP_DISH 'và' NP_DISH

# --- Hủy ---
VP_CANCEL -> V_CANCEL NP_DISH
VP_CANCEL -> V_CANCEL NP_DISH 'trong' NP_ORDER
VP_CANCEL -> V_CANCEL NP_ORDER
VP_CANCEL -> V_CANCEL 'toàn' 'bộ' 'đơn'
VP_CANCEL -> V_CANCEL NP_DISH 'khỏi' NP_ORDER
VP_CANCEL -> V_CANCEL 'tất' 'cả' 'món' 'trong' NP_ORDER

# --- Thêm ---
VP_ADD -> V_ADD NP_DISH
VP_ADD -> V_ADD NP_DISH 'vào' NP_ORDER
VP_ADD -> V_ADD 'ghi' 'chú' NP_DISH

# --- Sửa ---
VP_MODIFY -> V_MODIFY NP_DISH 'thành' NP_DISH
VP_MODIFY -> V_MODIFY 'số' 'lượng' NP_DISH 'thành' QUANTITY
VP_MODIFY -> V_MODIFY NP_DISH 'từ' QUANTITY 'thành' QUANTITY
VP_MODIFY -> V_MODIFY NP_DISH 'lên' QUANTITY
VP_MODIFY -> V_MODIFY 'giảm' 'số' 'lượng' NP_DISH 'xuống' QUANTITY

# --- Xác nhận ---
VP_CONFIRM -> V_CONFIRM NP_ORDER
VP_CONFIRM -> V_CONFIRM 'món' 'đã' 'đặt'

# --- Hỏi thông tin ---
VP_QUERY -> V_QUERY_EXIST | V_QUERY_PRICE | V_QUERY_MENU | V_QUERY_TIME | V_QUERY_STATUS

# --- Động từ ---
V_ORDER -> 'đặt' | 'lấy' | 'order' | 'gọi'
V_ORDER -> 'cho' SUBJ 'đặt' | 'em' 'đặt' | 'mình' 'đặt'

V_CANCEL -> 'hủy' | 'bỏ' | 'xóa'
V_CANCEL -> 'cho' SUBJ 'hủy' | 'em' 'hủy' | 'mình' 'hủy'

V_ADD -> 'thêm' | 'cho' 'thêm' | 'thêm' 'vào' 'đơn' | 'thêm' 'nữa' | 'gọi' 'thêm'

V_MODIFY -> 'đổi' | 'thay' 'đổi' | 'sửa' | 'chỉnh' | 'cập' 'nhật'
V_CONFIRM -> 'xác' 'nhận' | 'kiểm' 'tra' | 'xem' 'lại'

# --- Cụm từ truy vấn ---
V_QUERY_EXIST -> 'có' NP_DISH 'không'
V_QUERY_EXIST -> 'có' NP_DISH 'không' 'ạ'
V_QUERY_EXIST -> 'còn' NP_DISH 'không'
V_QUERY_EXIST -> VP_ORDER 'được' 'không'
V_QUERY_EXIST -> 'có' 'thể' VP_ORDER 'không'

V_QUERY_PRICE -> NP_DISH 'giá' 'bao' 'nhiêu'
V_QUERY_PRICE -> 'bao' 'nhiêu' 'tiền' 'một' NP_DISH
V_QUERY_PRICE -> NP_DISH 'bao' 'nhiêu' 'tiền'
V_QUERY_PRICE -> 'giá' NP_DISH
V_QUERY_PRICE -> NP_DISH 'của' 'là' 'bao' 'nhiêu'
V_QUERY_PRICE -> 'giá' NP_DISH 'là' 'bao' 'nhiêu'

V_QUERY_MENU -> 'menu' 'có' 'những' 'món' 'gì' |'có' 'những' 'món' 'nào' |'có' 'món' 'gì' |'xem' 'menu'

V_QUERY_TIME -> 'giao' 'hàng' 'mất' 'bao' 'lâu' | 'bao' 'giờ' 'giao' 'đến' | 'khi' 'nào' 'giao' | 'giao' 'sớm' 'hơn' | 'sớm' 'hơn'

V_QUERY_STATUS -> NP_ORDER 'của' NP_CUSTOMER 'đến' 'đâu' 'rồi'
V_QUERY_STATUS -> 'kiểm' 'tra' NP_ORDER
V_QUERY_STATUS -> NP_DISH 'đang' 'có' 'khuyến' 'mãi'
V_QUERY_STATUS -> 'món' 'nào' 'đang' 'có' 'khuyến' 'mãi'
V_QUERY_STATUS -> 'món' 'ăn' 'nào' 'ngon' 'nhất' PUNC_Q

# --- NP ---
NP -> NP_DISH | NP_ORDER | NP_CUSTOMER

# NP món
NP_DISH -> QUANTITY DISH NP_MOD | QUANTITY DISH | DISH NP_MOD | DISH | DISH 'này'

# NP đơn
NP_ORDER -> 'đơn' 'hàng' | 'đơn' | 'order' | 'món' 'đã' 'đặt' | 'toàn' 'bộ' 'đơn'
NP_ORDER -> 'thời' 'gian'
NP_ORDER -> 'tất' 'cả' NP_ORDER
NP_ORDER -> NP_ORDER 'này'

# NP khách hàng
NP_CUSTOMER -> SUBJ | 'em' | 'mình' | 'chúng' SUBJ

# NP bổ nghĩa
NP_MOD -> 'ít' 'đường' | 'nhiều' 'đá' | 'không' 'cay' | 'ít' 'đá' 'ít' 'đường'
NP_MOD -> 'nhiều' 'đường' | 'không' 'đá' | 'cay' 'vừa' | 'cay' 'nhiều'
NP_MOD -> 'size' 'lớn' | 'size' 'nhỏ' | 'nóng' | 'lạnh'
NP_MOD -> 'không' 'hành' | 'không' 'rau' | 'không' 'rau' 'thơm'
NP_MOD -> 'chay' | 'ngon' 'nhất' | 'rau' 'mùi' | 'nướng' | 'dự' 'kiến' | 'chanh' | 'vặt'

# --- Số lượng ---
QUANTITY -> NUMBER UNIT | NUMBER

NUMBER -> '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7'
NUMBER -> '8' | '9' | '10' | '11' | '12' | '13' | '14' | '15' | '16' | '17' | '18' | '19' | '20'
NUMBER -> '21' | '22' | '23'
NUMBER -> 'một' | 'hai' | 'ba' | 'bốn' | 'năm' | 'sáu' | 'bảy' | 'tám' | 'chín' | 'mười'

UNIT -> 'phần' | 'ly' | 'cốc' | 'suất' | 'chai' | 'tô' | 'dĩa' | 'đĩa' | 'miếng' | 'phần' 'ăn' | 'cái'

# --- Tên món ---
DISH -> 'phở' 'bò' | 'phở' 'gà' | 'bún' 'chả' | 'bún' 'bò' 'huế'
DISH -> 'bún' 'riêu' | 'bún' 'riêu' 'cua'
DISH -> 'cơm' 'rang' 'dưa' 'bò' | 'cơm' 'rang' 'hải' 'sản' | 'cơm' 'chiên' 'dương' 'châu' | 'cơm' 'tấm' 'sườn' 'bì' 'chả'
DISH -> 'cơm' 'gà' 'xối' 'mỡ' | 'gà' 'rán'
DISH -> 'mì' 'xào' 'giòn' | 'mì' 'xào' 'mềm'
DISH -> 'bánh' 'mì' 'thịt' | 'bánh' 'mì' 'pate'
DISH -> 'hủ' 'tiếu' 'nam' 'vang'
DISH -> 'trà' 'sữa' | 'trà' 'sữa' 'trân' 'châu'
DISH -> 'cà' 'phê' 'sữa' | 'cà' 'phê' 'đen'
DISH -> 'nước' 'cam' | 'nước' 'ép'
DISH -> 'sinh' 'tố' 'bơ' | 'sinh' 'tố' 'dâu'
DISH -> 'trà' 'đá' | 'coca'
DISH -> 'nem' 'rán' | 'chả' 'giò' | 'khoai' 'tây' 'chiên'
DISH -> 'lẩu' | 'gỏi' 'cuốn' | 'xôi' 'xéo'

# --- Thời gian ---
PP_TIME -> PREP_TIME TIME | 'ngay' | 'ngay' 'bây' 'giờ' | 'chiều' 'nay'
PREP_TIME -> 'lúc' | 'vào' | 'giao' 'lúc' | 'giao' 'vào' | 'vào' 'lúc'

TIME -> TIME_NUMBER 'giờ' TIME_PERIOD | TIME_NUMBER 'giờ' | TIME_NUMBER 'giờ' MINUTE
TIME_PERIOD -> 'sáng' | 'chiều' | 'tối' | 'trưa' | 'buổi' 'sáng' | 'buổi' 'chiều' | 'buổi' 'tối'
TIME_NUMBER -> '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' | '10' | '11' | '12'
TIME_NUMBER -> '13' | '14' | '15' | '16' | '17' | '18' | '19' | '20' | '21' | '22' | '23'
MINUTE -> '00' | '10' | '15' | '20' | '30' | '45' | '50'

# --- Địa điểm ---
PP_LOCATION -> PREP_LOCATION LOCATION
PREP_LOCATION -> 'đến' | 'tới' | 'giao' 'đến' | 'giao' 'tới' | 'về'
LOCATION -> 'số' NUMBER 'đường' STREET_NAME | 'khu' NUMBER | 'tầng' NUMBER | 'phòng' NUMBER | 'quận' NUMBER
STREET_NAME -> 'Nguyễn' 'Trãi' | 'Lê' 'Lợi' | 'Trần' 'Phú' | 'Hai' 'Bà' 'Trưng'
